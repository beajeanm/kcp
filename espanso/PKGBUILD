pkgname=espanso
pkgver=0.6.2
pkgrel=1
pkgdesc="Cross-platform Text Expander written in Rust"
arch=(x86_64)
url="https://espanso.org/"
license=("GPL3")
depends=("xdotool" "xclip" "libxtst" "libnotify")
makedepends=("rust" "git" "cmake")
install="${pkgname}.install"
source=("${pkgname}::git+https://github.com/federico-terzi/espanso.git#tag=v${pkgver}")
sha512sums=('SKIP')


prepare() {
    cd "$pkgname"

    # don't change the original service file, as it will be embedded in the binary
    cp "src/res/linux/systemd.service" "systemd.service"
    sed -i "s|{{{espanso_path}}}|/usr/bin/espanso|g" "systemd.service"
}

check() {
    cd "$pkgname"

    cargo test --release --locked
}

build() {
    cd "$pkgname"

    cargo build --release --locked
}

package() {
    cd "$pkgname"

    install -Dm755 "target/release/${pkgname}" "${pkgdir}/usr/bin/${pkgname}"
    install -Dm644 "systemd.service" "${pkgdir}/usr/lib/systemd/user/${pkgname}.service" # install our own copy

    install -Dm644 "README.md" "${pkgdir}/usr/share/doc/${pkgname}/README.md"
}
